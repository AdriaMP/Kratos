@Behaviour Elasticity;
@Author Helfer Thomas;
@Date 23/11/06;
@Description{
 A very first test 
 (the most simple one in fact).
}

// Material properties
@Parameter stress young = 150e9;
young.setGlossaryName("YoungModulus");
@Parameter real   nu = 0.3;
nu.setGlossaryName("PoissonRatio");

// Lame Coefficients
@LocalVariable stress lambda,mu;

@InitLocalVariables{
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
}

@PredictionOperator{
  static_cast<void>(smt); // remove unused variable warning
  computeAlteredElasticStiffness<hypothesis,real>::exe(Dt,lambda,mu);
}

@Integrator{
  sig = lambda*trace(eto+deto)*StrainStensor::Id()+2*mu*(eto+deto);
  if(computeTangentOperator_){
    Dt = lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
  }
}
